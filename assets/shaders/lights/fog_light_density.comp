#version 450
layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

#include "volumetric_fog.h"
#include "lighting_scatter.h"
#include "../inc/render_parameters.h"

layout(push_constant, std430) uniform Registers
{
    mat4 inv_view_projection;
    vec4 z_transform;
    uvec3 count;
    vec3 inv_resolution;
    float in_scatter_strength;
    vec2 xy_scale;
    float slice_z_log2_scale;
    float density_mod;
} registers;

float to_clip_z(float world_z)
{
    vec2 zw = registers.z_transform.zw - registers.z_transform.xy * world_z;
    return zw.x / zw.y;
}

layout(rgba16f, set = 2, binding = 0) writeonly uniform image3D uLightDensity;
layout(std140, set = 2, binding = 1) uniform BlockExtents
{
    vec4 slice_extents[1024 / 4];
};

vec3 get_world_position(vec3 uvw)
{
    float world_z = volumetric_fog_texture_z_to_world(uvw.z, registers.slice_z_log2_scale);
    float clip_z = to_clip_z(world_z);
    vec4 clip = registers.inv_view_projection * vec4(uvw.xy * 2.0 - 1.0, clip_z, 1.0);
    vec3 pos = clip.xyz / clip.w;
    return pos;
}

float get_fog_albedo(vec3 uvw)
{
    // TODO: Modulate density based on pos.
    float length_mod = length(vec3(1.0, (uvw.xy * 2.0 - 1.0) * registers.xy_scale));
    float albedo = registers.density_mod * slice_extents[gl_WorkGroupID.z][gl_LocalInvocationID.z] * length_mod;
    return albedo;
}

vec3 compute_inscatter_light(vec3 pos)
{
#ifdef SHADOWS
#ifdef SHADOW_CASCADES
    vec4 clip_shadow_near = shadow.near * vec4(pos, 1.0);
#else
    vec4 clip_shadow_near = vec4(0.0);
#endif
    vec4 clip_shadow_far = shadow.far * vec4(pos, 1.0);
#endif

    return compute_scatter_lighting(
        pos,
        global.camera_position,
        global.camera_front,
        directional.direction,
        directional.color
#ifdef SHADOWS
		, clip_shadow_near, clip_shadow_far, shadow.inv_cutoff_distance
#endif
#ifdef ENVIRONMENT
        , environment.intensity
#endif
    );
}

void main()
{
    if (any(greaterThanEqual(gl_GlobalInvocationID, registers.count)))
        return;
    vec3 uvw = (vec3(gl_GlobalInvocationID) + 0.5) * registers.inv_resolution;
    vec3 pos = get_world_position(uvw);
    float albedo = get_fog_albedo(uvw);
    vec3 in_scatter_light = registers.in_scatter_strength * compute_inscatter_light(pos);

    imageStore(uLightDensity, ivec3(gl_GlobalInvocationID), vec4(in_scatter_light * albedo, albedo));
}